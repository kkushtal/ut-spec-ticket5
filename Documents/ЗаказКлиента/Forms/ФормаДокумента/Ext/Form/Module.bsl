// Заполняет реквизит "НижнийПорогРентабельности" в товарной табличной части.
//
// Параметры:
// 	 МассивСтрок - Массив, Неопределено - Массив выбранных строк,
//										 если Неопределено, то будут заполнены ВСЕ строки.
//
&НаСервере
Процедура РассчитатьНижнийПорогРентабельности(МассивСтрок = Неопределено)
	ВидЦены = Константы.ВидМинимальноДопустимыхЦенПродажи.Получить();
	
	ПараметрыЗаполнения = Новый Структура();
	ПараметрыЗаполнения.Вставить("Дата", Объект.Дата);
	ПараметрыЗаполнения.Вставить("Организация", Объект.Организация);
	ПараметрыЗаполнения.Вставить("Валюта", Объект.Валюта);
	ПараметрыЗаполнения.Вставить("ВидЦены", ВидЦены);
	ПараметрыЗаполнения.Вставить("РассчитыватьНаборы", Истина);
	ПараметрыЗаполнения.Вставить("ПоляЗаполнения", "НижнийПорогРентабельности");
	ПараметрыЗаполнения.Вставить("ПолеЦена", "НижнийПорогРентабельности");
	
	ЦеныПредприятияЗаполнениеСервер.ЗаполнитьЦены(Объект.Товары, МассивСтрок, ПараметрыЗаполнения);
КонецПроцедуры

&НаСервере
Процедура РассчитатьНижнийПорогРентабельностиТекущейСтроки()
	ТекущаяСтрока = Объект.Товары.НайтиПоИдентификатору(Элементы.Товары.ТекущаяСтрока);
	МассивСтрок = Новый Массив();
	МассивСтрок.Добавить(ТекущаяСтрока);
	РассчитатьНижнийПорогРентабельности(МассивСтрок);
КонецПроцедуры

&НаСервере
&После("ОбработкаВыбораПодборНаСервере")
Процедура экз_ОбработкаВыбораПодборНаСервере(ВыбранноеЗначение)
	МассивСтрок = Новый Массив;
	Для Каждого Строка Из Объект.Товары Цикл
		Если НЕ ЗначениеЗаполнено(Строка.НижнийПорогРентабельности) Тогда
			МассивСтрок.Добавить(Строка);
		КонецЕсли;
	КонецЦикла;
	РассчитатьНижнийПорогРентабельности(МассивСтрок);
КонецПроцедуры

&НаКлиенте
Процедура экз_ДатаПриИзмененииПосле(Элемент)
	РассчитатьНижнийПорогРентабельности();
КонецПроцедуры

&НаКлиенте
Процедура экз_ВалютаПриИзмененииПосле(Элемент)
	РассчитатьНижнийПорогРентабельности();
КонецПроцедуры

&НаКлиенте
Процедура экз_ТоварыНоменклатураПриИзмененииПосле(Элемент)
	РассчитатьНижнийПорогРентабельностиТекущейСтроки();
КонецПроцедуры

&НаКлиенте
Процедура экз_ТоварыХарактеристикаПриИзмененииПосле(Элемент)
	РассчитатьНижнийПорогРентабельностиТекущейСтроки();
КонецПроцедуры

&НаКлиенте
Процедура экз_ТоварыУпаковкаПриИзмененииПосле(Элемент)
	РассчитатьНижнийПорогРентабельностиТекущейСтроки();
КонецПроцедуры

&НаКлиенте
Процедура экз_ТоварыСерияПриИзмененииПосле(Элемент)
	РассчитатьНижнийПорогРентабельностиТекущейСтроки();
КонецПроцедуры

&НаКлиенте
Процедура экз_ТоварыВидЦеныПриИзмененииПосле(Элемент)
	РассчитатьНижнийПорогРентабельностиТекущейСтроки();
КонецПроцедуры


// https://its.1c.ru/db/pubdevguide83#content:617:hdoc
// Поместил в ОбработкаПроверкиЗаполненияНаСервереПосле, т.к.
// - можно проверить на этапе формы (до записи данных в БД), иначе пришлось бы делать запрос в ОбработкеПроведения после записи в БД и отменять транзакцию, а это лишнее.
// - не вызывается при Удалении проведения, 
// - не вызывается при сохранении (записи) документа
// - и т.к. семантически проверка ближе всего к ПроверкеЗаполнения, но проверка не только на заполненность, а на соответствие условию Цена > НижнийПорогРентабельности
// 
&НаСервере
Процедура экз_ОбработкаПроверкиЗаполненияНаСервереПосле(Отказ, ПроверяемыеРеквизиты)
	Если НЕ Отказ и РольДоступна("ЗапретПродажиНижеЦеныРентабельности") Тогда
		Для Каждого Строка Из Объект.Товары Цикл
			Если ЗначениеЗаполнено(Строка.Цена) И ЗначениеЗаполнено(Строка.НижнийПорогРентабельности)
				И Строка.Цена < Строка.НижнийПорогРентабельности Тогда
				Отказ = Истина;
				
				СтрНоменклатура = "номенклатуру: " + Строка.Номенклатура;
				СтрХарактеристика = ?(ЗначениеЗаполнено(Строка.Характеристика), ", характеристика: " + Строка.Характеристика, "");
				СтрСерия = ?(ЗначениеЗаполнено(Строка.Серия), ", серия: " + Строка.Серия, "");
				СтрУпаковка = ?(ЗначениеЗаполнено(Строка.Упаковка), ", упаковка: " + Строка.Упаковка, "");
				ШаблонСтроки = "Нельзя продать %1%2%3%4, по цене %5 ниже порога рентабельности %6.";
				
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСтроки, 
				СтрНоменклатура, СтрХарактеристика, СтрСерия, СтрУпаковка, Строка.Цена, Строка.НижнийПорогРентабельности);
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,,Отказ);
			КонецЕсли;
		КонецЦикла;   
	КонецЕсли;
КонецПроцедуры
